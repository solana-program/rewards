# Install dependencies
install:
    pnpm install

# Generate IDL from Rust code using Codama
generate-idl:
    @echo "Generating IDL..."
    pnpm run generate-idl

# Generate clients from IDL using Codama
generate-clients: generate-idl
    @echo "Generating clients..."
    pnpm run generate-clients

# Build the program
build: generate-idl generate-clients
    cd program && cargo-build-sbf

# Format / lint code
fmt:
    cargo fmt -p rewards-program -p tests-rewards-program
    @cd program && cargo clippy --all-targets -- -D warnings
    @cd tests && cargo clippy --all-targets -- -D warnings
    pnpm format
    pnpm lint:fix

check:
    cd program && cargo check --features idl
    pnpm run format:check
    pnpm lint

# Run unit tests
unit-test:
    cargo test -p rewards-program

# Run integration tests (use --with-cu to track compute units and update README)
integration-test *args:
    #!/usr/bin/env bash
    set -e
    if [[ "{{ args }}" == *"--with-cu"* ]]; then
    	./scripts/integration-test-with-cu.sh
    else
    	cargo test -p tests-rewards-program "$@"
    fi

# Run all tests (use --with-cu to track compute units)
test *args: build unit-test (integration-test args)

# Build Client for Examples
build-client:
    pnpm run generate-clients
    cd clients/typescript && pnpm build
    cd examples/typescript/rewards-demo && pnpm install

# ******************************************************************************
# Deployment (requires txtx CLI: https://docs.txtx.sh/install)
# ******************************************************************************

[private]
check-txtx:
    @command -v txtx >/dev/null 2>&1 || { echo "Error: txtx not found. Install from: https://docs.txtx.sh/install"; exit 1; }

# Deploy to devnet (supervised mode with web UI)
deploy-devnet: check-txtx
    txtx run deploy --env devnet

# Deploy to devnet (unsupervised/CI mode)
deploy-devnet-ci: check-txtx
    txtx run deploy --env devnet -u

# Deploy to localnet (for testing with local validator)
deploy-localnet: check-txtx
    txtx run deploy --env localnet

# ******************************************************************************
# IDL Deployment (uses Program Metadata Program)
# ******************************************************************************

[private]
check-program-metadata:
    @command -v program-metadata >/dev/null 2>&1 || { echo "Error: program-metadata not installed. See https://github.com/solana-program/program-metadata"; exit 1; }

# Deploy IDL to devnet
deploy-idl-devnet: check-program-metadata
    program-metadata write idl 7kw4iaikc9qTaFGcWx4wDiCXkkLddTb65HV8xH7KbHyc idl/rewards_program.json \
        --keypair .keypairs/rewards-devnet-deployer.json \
        --rpc https://api.devnet.solana.com

# Deploy IDL to mainnet
deploy-idl-mainnet: check-program-metadata
    program-metadata write idl 7kw4iaikc9qTaFGcWx4wDiCXkkLddTb65HV8xH7KbHyc idl/rewards_program.json \
        --keypair .keypairs/rewards-mainnet-deployer.json \
        --rpc https://api.mainnet-beta.solana.com

# ******************************************************************************
# Build Verification (uses solana-verify CLI)
# ******************************************************************************

[private]
check-solana-verify:
    @command -v solana-verify >/dev/null 2>&1 || { echo "Error: solana-verify not installed. Run: cargo install solana-verify"; exit 1; }

# Verify mainnet deployment against repo (remote build via OtterSec)

# Note: Remote verification (--remote) only works on mainnet
verify-mainnet: check-solana-verify
    solana-verify verify-from-repo \
        https://github.com/solana-program/rewards \
        --program-id 7kw4iaikc9qTaFGcWx4wDiCXkkLddTb65HV8xH7KbHyc \
        --library-name rewards_program \
        --mount-path program \
        --remote \
        -um

# ******************************************************************************
# Release
# ******************************************************************************

# Prepare a new release (bumps versions, generates changelog)
[confirm('Start release process?')]
release:
    #!/usr/bin/env bash
    set -euo pipefail

    if [ -n "$(git status --porcelain)" ]; then
    	echo "Error: Working directory not clean"
    	exit 1
    fi

    command -v git-cliff &>/dev/null || { echo "Install git-cliff: cargo install git-cliff"; exit 1; }

    # Get current versions
    rust_version=$(grep "^version" clients/rust/Cargo.toml | head -1 | sed 's/version = "\(.*\)"/\1/')
    ts_version=$(node -p "require('./clients/typescript/package.json').version")

    echo "Current versions:"
    echo "  Rust client:       $rust_version"
    echo "  TypeScript client: $ts_version"
    echo ""

    read -p "New version: " version
    [ -z "$version" ] && { echo "Version required"; exit 1; }

    echo "Updating to $version..."

    # Update Rust client version
    sed -i.bak "s/^version = \".*\"/version = \"$version\"/" clients/rust/Cargo.toml
    rm -f clients/rust/Cargo.toml.bak

    # Update TypeScript client version
    cd clients/typescript && npm version "$version" --no-git-tag-version --allow-same-version
    cd ../..

    echo "Generating CHANGELOG..."
    last_tag=$(git tag -l "v*" --sort=-version:refname | head -1)
    if [ -z "$last_tag" ]; then
    	git-cliff --config .github/cliff.toml --tag "v$version" --output CHANGELOG.md --strip all
    elif [ -f CHANGELOG.md ]; then
    	git-cliff "$last_tag"..HEAD --tag "v$version" --config .github/cliff.toml --strip all > CHANGELOG.new.md
    	cat CHANGELOG.md >> CHANGELOG.new.md
    	mv CHANGELOG.new.md CHANGELOG.md
    else
    	git-cliff "$last_tag"..HEAD --tag "v$version" --config .github/cliff.toml --output CHANGELOG.md --strip all
    fi

    git add clients/rust/Cargo.toml clients/typescript/package.json CHANGELOG.md

    echo ""
    echo "Ready! Next steps:"
    echo "  git commit -m 'chore: release v$version'"
    echo "  git push origin HEAD"
    echo "  Trigger 'Publish Rust Client' and 'Publish TypeScript Client' workflows"
